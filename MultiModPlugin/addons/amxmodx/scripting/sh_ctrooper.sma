 //CloneTrooper

 //Credits go to Vittu for Master Chief´s Morphing Code and Teh Creators of Cyclops
 
 /* CVARS - copy and paste to shconfig.cfg

 //CTrooper
 CTrooper_level 10
 CTrooper_teamglow 0        //Glow Team Color when player skin in use (0=no 1=yes)
 CTrooper_ak47mult 2.0            //Damage multiplyer for his Ak47
 CTrooper_health 150        //Default 150
 CTrooper_armor 150        //Default 150
 CTrooper_teamcolored 1    //Default 1,

 //Version 1.0 - Added hero and Added Player and Gun Model (First Created
 //Version 1.1 - Added Laser shots to gun to add more Realism to the Star Wars factor
 //Version 1.2 - Re-Coded and Compiled for AMXX to work again...(Permissiobn or Om3gA
 //Version 1.3 - Re-Coded and addet Laser bullets
 //Version 2.0 - Re-Codet and addet Laser bullets that work
 //Version 2.1 - Ad-Codes he works with the laser and gives you a free AK_47
 //Version 2.2 - Ad-Codes addet unlimited bullets
 //Version 2.3 - Ad-Codes/mdl addet a p mdl and he spawns with an AK_47 when you re-spawn
 //Version 2.4 - Ad-Codes Addet a new mdl and teamcolored lasers thx to JTP
 //version 2.5 - Cleand up the code
 //Version 2.6 - Fixed teh Laser Tracer So now Teh Laser Is Where Ya Shoot Removed Xtra Speed And Gravity And made a CZ Version
 */

 #include <amxmod>
 #include <vexd_utilities>
 #include <superheromod>

 // GLOBAL VARIABLES
 new gHeroName[]="Clone Trooper"
 new bool:gHasCTrooperPower[SH_MAXSLOTS+1]
 new bool:gmorphed[SH_MAXSLOTS+1]
 new gTaskID
 new gCTrooperSound[]="items/suitchargeno1.wav"
 new lastammo[33]
 new spriteindex, smoke
 //----------------------------------------------------------------------------------------------
 public plugin_init()
 {
	// Plugin Info
	register_plugin("SUPERHERO Clone Trooper","2.6","Om3gA")

	// DO NOT EDIT THIS FILE TO CHANGE CVARS, USE THE SHCONFIG.CFG
	register_cvar("ctrooper_level", "0")
	register_cvar("ctrooper_teamglow", "0")
	register_cvar("ctrooper_ak47mult", "2.0")
	register_cvar("ctrooper_health", "150")
	register_cvar("ctrooper_armor", "150")
	register_cvar("ctrooper_teamcolored", "1")
	// FIRE THE EVENT TO CREATE THIS SUPERHERO!
	shCreateHero(gHeroName, "CTrooper", "Become a Clone Trooper! - Cooler Player Model and Gun and laser bullets", false, "CTrooper_level" )

	// REGISTER EVENTS THIS HERO WILL RESPOND TO! (AND SERVER COMMANDS)

	// INIT
	register_srvcmd("CTrooper_init", "CTrooper_init")
	shRegHeroInit(gHeroName, "CTrooper_init")
	register_event("CurWeapon", "make_tracer", "be", "1=1", "3>0")
	register_event("CurWeapon", "weaponChange", "be", "1=1")
	register_event("ResetHUD", "newSpawn", "b")
	register_event("Damage", "CTrooper_damage", "b", "2!0")
	register_event("DeathMsg", "CTrooper_death", "a")
	
	// Let Server know about Master Chief's Variables
	shSetShieldRestrict(gHeroName)
	shSetMaxHealth(gHeroName, "ctrooper_health")
	shSetMaxArmor(gHeroName, "ctrooper_armor")
 }
 //----------------------------------------------------------------------------------------------
 public plugin_precache()
 {
	smoke = precache_model("sprites/steam1.spr")
	spriteindex = precache_model("sprites/laserbeam.spr")
	precache_sound("weapons/electro5.wav")
	precache_model("models/player/clonetrooper/clonetrooper.mdl")
	precache_model("models/shmod/clonetrooper_v_ak47.mdl")
	precache_model("models/shmod/clonetrooper_p_ak47.mdl")
	precache_sound(gCTrooperSound)
 }
 //----------------------------------------------------------------------------------------------
 public CTrooper_init()
 {
	// First Argument is an id
	new temp[6]
	read_argv(1,temp,5)
	new id = str_to_num(temp)

	// 2nd Argument is 0 or 1 depending on whether the id has CTrooper
	read_argv(2,temp,5)
	new hasPowers = str_to_num(temp)

	gHasCTrooperPower[id] = (hasPowers != 0)

	//Reset thier shield restrict status
	//Shield restrict MUST be before weapons are given out
	shResetShield(id)

	if ( is_user_connected(id) ) {
		if ( gHasCTrooperPower[id] ) {
			CTrooper_weapons(id)
			switchmodel(id)
			CTrooper_tasks(id)
		}
		else {
			engclient_cmd(id, "drop", "weapon_ak47")
			CTrooper_unmorph(id)
			shRemHealthPower(id)
			shRemArmorPower(id)
			shRemGravityPower(id)
			shRemSpeedPower(id)
		}
	}
 }
 //----------------------------------------------------------------------------------------------
 public make_tracer(id)
 {
	if (!gHasCTrooperPower[id]) return PLUGIN_CONTINUE
	new clip, ammo
	new wpnid = get_user_weapon(id, clip, ammo)
	new pteam[16]

	get_user_team(id, pteam, 15)

	if ((lastammo[id] > clip) && (wpnid == CSW_AK47) ) {

		new vec1[3], vec2[3]
		get_user_origin(id, vec1, 1) // origin; your camera point.
		get_user_origin(id, vec2, 4) // termina; where your bullet goes (4 is cs-only)

		emit_sound(id,CHAN_ITEM, "weapons/electro5.wav", 1.0, ATTN_NORM, 0, PITCH_NORM)

		// DELIGHT
		message_begin( MSG_BROADCAST,SVC_TEMPENTITY)
		write_byte( 27 )
		write_coord( vec1[0] ) //pos
		write_coord( vec1[1] )
		write_coord( vec1[2] )
		write_byte( 10 )
		write_byte( 250 ) // r, g, b
		write_byte( 0 ) // r, g, b
		write_byte( 0 ) // r, g, b
		write_byte( 2 ) // life
		write_byte( 1 ) // decay
		message_end()

		//BEAMENTPOINTS
		message_begin( MSG_BROADCAST,SVC_TEMPENTITY)
		write_byte (0)     //TE_BEAMENTPOINTS 0
		write_coord(vec1[0])
		write_coord(vec1[1])
		write_coord(vec1[2])
		write_coord(vec2[0])
		write_coord(vec2[1])
		write_coord(vec2[2])
		write_short(spriteindex)
		write_byte(1) // framestart
		write_byte(5) // framerate
		write_byte(2) // life
		write_byte(10) // width
		write_byte(0) // noise
		if (!get_cvar_num("ctrooper_teamcolored")) {
			write_byte( 0 )     // r, g, b
			write_byte( 250 )       // r, g, b
			write_byte( 0 )       // r, g, b
		}
		// Terrorist
		else if (get_user_team(id)==1) {
			write_byte( 255 )     // r, g, b
			write_byte( 0 )       // r, g, b
			write_byte( 0 )       // r, g, b
		}
		// Counter-Terrorist
		else {
			write_byte( 0 )      // r, g, b
			write_byte( 255 )      // r, g, b
			write_byte( 0 )    // r, g, b
		}
		write_byte(200) // brightness
		write_byte(200) // speed
		message_end()

		//Sparks
		message_begin( MSG_PVS, SVC_TEMPENTITY)
		write_byte(9)
		write_coord(vec2[0])
		write_coord(vec2[1])
		write_coord(vec2[2])
		message_end()
		//Smoke
		message_begin( MSG_BROADCAST,SVC_TEMPENTITY)
		write_byte(5) // 5
		write_coord(vec2[0])
		write_coord(vec2[1])
		write_coord(vec2[2])
		write_short(smoke)
		write_byte(22)  // 10
		write_byte(10)  // 10
		message_end()
	}

	lastammo[id] = clip

	return PLUGIN_CONTINUE
 }
 //----------------------------------------------------------------------------------------------
 public newSpawn(id)
 {
	if ( gHasCTrooperPower[id] && is_user_alive(id) && shModActive() ) {
		set_task(0.1, "CTrooper_weapons", id)
		CTrooper_tasks(id)

		new clip, ammo, wpnid = get_user_weapon(id,clip,ammo)
		if (wpnid != CSW_AK47 && wpnid > 0) {
			new wpn[32]
			get_weaponname(wpnid,wpn,31)
			engclient_cmd(id,wpn)
		}
	}
 }
 //----------------------------------------------------------------------------------------------
 public CTrooper_tasks(id)
 {
	set_task(1.0, "CTrooper_morph", id)

	if( get_cvar_num("ctrooper_teamglow") ){
		set_task(1.0, "CTrooper_glow", id+gTaskID, "", 0, "b" )
	}

 }
 //----------------------------------------------------------------------------------------------
 public switchmodel(id)
 {
	if ( !is_user_alive(id) || !gHasCTrooperPower[id] ) return

	new clip, ammo, wpnid = get_user_weapon(id, clip, ammo)

	if (wpnid == CSW_AK47) {

		Entvars_Set_String(id, EV_SZ_viewmodel, "models/shmod/clonetrooper_v_ak47.mdl")
		// Weapon Model change for 3rd person view - vittu
		Entvars_Set_String(id, EV_SZ_weaponmodel, "models/shmod/clonetrooper_p_ak47.mdl")
	}
 }
 //----------------------------------------------------------------------------------------------
 public weaponChange(id)
 {
	if ( !gHasCTrooperPower[id] || !shModActive() ) return

	new wpnid = read_data(2)
	new clip = read_data(3)

	if ( wpnid != CSW_AK47 ) return

	switchmodel(id)

	// Never Run Out of Ammo!
	if ( clip == 0 ) {
		shReloadAmmo(id)
	}
 }
 //----------------------------------------------------------------------------------------------
 public CTrooper_weapons(id)
 {
	if ( is_user_alive(id) ) {
		shGiveWeapon(id,"weapon_ak47")
	}
 }
 //----------------------------------------------------------------------------------------------
 public CTrooper_damage(id)
 {
	if (!shModActive() || !is_user_alive(id)) return

	new damage = read_data(2)
	new weapon, bodypart, attacker = get_user_attacker(id, weapon, bodypart)
	new headshot = bodypart == 1 ? 1 : 0

	if ( attacker <= 0 || attacker > SH_MAXSLOTS ) return

	if ( gHasCTrooperPower[attacker] && weapon == CSW_AK47 && is_user_alive(id) ) {
		if ( gHasCTrooperPower[attacker] && is_user_alive(id) ) {
			// do extra damage
			new extraDamage = floatround(damage * get_cvar_float("ctrooper_ak47mult") - damage)
			if (extraDamage > 0) shExtraDamage( id, attacker, extraDamage, "ak47", headshot )
		}
	}
 }
 //----------------------------------------------------------------------------------------------
 public CTrooper_sound(id)
 {
	emit_sound(id, CHAN_AUTO, gCTrooperSound, 0.2, ATTN_NORM, 0, PITCH_NORM)
	emit_sound(id, CHAN_AUTO, gCTrooperSound, 0.2, ATTN_NORM, 0, PITCH_NORM)
 }
 //----------------------------------------------------------------------------------------------
 public CTrooper_morph(id)
 {
	if ( gmorphed[id] || !is_user_alive(id) ) return

	#if defined AMXX_VERSION
	cs_set_user_model(id, "clonetrooper")
	#else
	CS_SetModel(id, "clonetrooper")
	#endif

	CTrooper_sound(id)

	// Message
	set_hudmessage(50, 205, 50, -1.0, 0.40, 2, 0.02, 4.0, 0.01, 0.1, 7)
	show_hudmessage(id, "CTrooper - Prepared for war..SIR!")

	gmorphed[id] = true
 }
 //----------------------------------------------------------------------------------------------
 public CTrooper_unmorph(id)
 {
	if ( gmorphed[id] ) {
		// Message
		set_hudmessage(50, 205, 50, -1.0, 0.40, 2, 0.02, 4.0, 0.01, 0.1, 7)
		show_hudmessage(id, "CTrooper - All systems down...")

		#if defined AMXX_VERSION
		cs_reset_user_model(id)
		#else
		CS_ClearModel(id)
		#endif

		CTrooper_sound(id)

		gmorphed[id] = false

		if ( get_cvar_num("ctrooper_teamglow") ) {
			remove_task(id+gTaskID)
			set_user_rendering(id)
		}
	}
 }
 //----------------------------------------------------------------------------------------------
 public CTrooper_glow(id)
 {
	id -= gTaskID

	if ( !is_user_connected(id) ) {
		//Don't want any left over residuals
		remove_task(id+gTaskID)
		return
	}

	if ( gHasCTrooperPower[id] && is_user_alive(id)) {
		if ( get_user_team(id) == 1 ) {
			shGlow(id, 255, 0, 0)
		}
		else {
			shGlow(id, 0, 0, 255)
		}
	}
 }
 //----------------------------------------------------------------------------------------------
 public CTrooper_death()
 {
	new id = read_data(2)
	CTrooper_unmorph(id)
 }
 //----------------------------------------------------------------------------------------------
 public client_connect(id)
 {
	gmorphed[id] = false
 }

